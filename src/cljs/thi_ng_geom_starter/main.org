-*- mode: org; mode: visual-line; cider-buffer-ns: "thi-ng-geom-starter.main" -*-
#+STARTUP: indent
#+PROPERTY: header-args:clojure  :tangle main.cljs
#+PROPERTY: header-args:clojure+ :results value verbatim replace

* Contents                                                          :TOC_2_org:
 - [[Links][Links]]
   - [[Geom][Geom]]
 - [[Setup][Setup]]
 - [[Stream Setup][Stream Setup]]
 - [[Model: Colour-Face Lighting Cube][Model: Colour-Face Lighting Cube]]
 - [[COMMENT Model: Texture-Mapped Cube][COMMENT Model: Texture-Mapped Cube]]
   - [[Shader][Shader]]
   - [[Model][Model]]
 - [[Viewport Machinery][Viewport Machinery]]
 - [[Setup (File Texture Version)][Setup (File Texture Version)]]
 - [[Loop (File Texture Version)][Loop (File Texture Version)]]

* Links
** Geom

- [[https://github.com/thi-ng/geom/blob/develop/src/gl/core.org][thi.ng.geom.core]] (~gl~) - [[https://gitter.im/thi-ng/geom][discussion]]
- [[https://github.com/thi-ng/geom/blob/develop/src/gl/buffers.org][thi.ng.geom.buffers]] (~buf~)

* Setup

#+BEGIN_SRC clojure
  (ns thi-ng-geom-starter.main
    (:require [reagent.core :as reagent]
              [thi.ng.math.core :as m :refer [PI HALF_PI TWO_PI]]
              [thi.ng.geom.gl.core :as gl]
              [thi.ng.geom.gl.webgl.constants :as glc]
              [thi.ng.geom.gl.webgl.animator :as anim]
              [thi.ng.geom.gl.buffers :as buf]
              [thi.ng.geom.gl.fx :as fx]
              [thi.ng.geom.gl.shaders :as sh]
              [thi.ng.geom.gl.glmesh :as glm]
              [thi.ng.geom.gl.camera :as cam]
              [thi.ng.geom.gl.shaders :as sh]
              [thi.ng.geom.gl.shaders.lambert :as lambert]
              [thi.ng.geom.core :as g]
              [thi.ng.geom.vector :as v :refer [vec2 vec3]]
              [thi.ng.geom.matrix :as mat :refer [M44]]
              [thi.ng.geom.aabb :as a]
              [thi.ng.geom.attribs :as attr]
              [thi.ng.domus.core :as dom]
              [thi.ng.color.core :as col]
              [thi.ng.strf.core :as f]
              [thi-ng-geom-starter.shaders :as shaders])
      (:require-macros [cljs-log.core :refer [debug info warn severe]]))
#+END_SRC

#+RESULTS:
: nil

Application state. This used to be just set once, replaced via ~reset!~ when the initialisation was called on Reagent component mount. Now it's a bit smarter: we swap in the video stream once (if) it kicks off. (It's a Reagent atom so that we can show its state on-screen.)

#+BEGIN_SRC clojure
  (defonce app (reagent/atom {:stream {:state :wait}
                              :curr-shader :thresh}))
#+END_SRC

#+RESULTS:
: nil

* Stream Setup

Helper: set stream state:

#+BEGIN_SRC clojure
  (defn set-stream-state! [state]
    (swap! app assoc-in [:stream :state] state))
#+END_SRC

Set up video texture. Requires a GL context.

#+BEGIN_SRC clojure
  (defn init-video-texture [video]
    (let [tex (buf/make-canvas-texture
               (:gl @app)
               video
               {:filter      glc/linear
                :wrap        glc/clamp-to-edge
                :width       (.-width video)
                :height      (.-height video)
                :flip        true
                :premultiply false})]
      (debug "SWAPPING!")
      (swap! app assoc-in [:scene :img :shader :state :tex] tex)))
#+END_SRC

Activate the RTC stream.

#+BEGIN_SRC clojure
  (defn activate-rtc-stream [video stream]
    (swap! app assoc-in [:stream :video] video)
    (set! (.-onerror video)
          (fn [] (.stop stream) (set-stream-state! :error)))
    (set! (.-onended stream)
          (fn [] (.stop stream) (set-stream-state! :stopped)))
    (set! (.-src video)
          (.createObjectURL (or (aget js/window "URL") (aget js/window "webkitURL")) stream))
    (set-stream-state! :ready)
    (init-video-texture video))
#+END_SRC

Stream initialisation. This appears to kick off a hidden video player anchored to the document body.

#+BEGIN_SRC clojure
  (defn init-rtc-stream [w h]
    (let [video (dom/create-dom!
                 [:video {:width w :height h :hidden true :autoplay true}]
                 (.-body js/document))]
      (cond
        (aget js/navigator "webkitGetUserMedia")
        (.webkitGetUserMedia js/navigator #js {:video true}
                             #(activate-rtc-stream video %)
                             #(set-stream-state! :forbidden))

        (aget js/navigator "mozGetUserMedia")
        (.mozGetUserMedia js/navigator #js {:video true}
                          #(activate-rtc-stream video %)
                          #(set-stream-state! :forbidden))

        :else
        (set-stream-state! :unavailable))))
#+END_SRC

* Model: Colour-Face Lighting Cube

Create model. We don't link in the camera here, but instead bind that in the update loop, to accomodate window resizing.

Attempting a bit of ~g/into~. It looks as if the first mesh (only) can carry the attributes. (Indexed mesh size has to encompass the entire merge.) Not sure why we can't colour the second cube.

#+BEGIN_SRC clojure
  (defn cube-mesh [tx]
    (-> (a/aabb 0.8)
        (g/center)
        (g/translate tx)
        (g/as-mesh
         {:mesh    (glm/indexed-gl-mesh (* 12 2) #{:col :fnorm})
          :flags   :ewfbs
          :attribs {:col (->> [[1 0 0] [0 1 0] [0 0 1] [0 1 1] [1 0 1] [1 1 0]]
                              (map col/rgba)
                              (attr/const-face-attribs))}})
  ))

  (defn make-model [gl]
    (-> (cube-mesh [0.1 0.1 0.5])
        (g/into (-> (a/aabb 0.8)
                    (g/center)
                    (g/as-mesh {:flags :ewfbs})))
        (gl/as-gl-buffer-spec {})
        (assoc :shader (sh/make-shader-from-spec gl lambert/shader-spec-two-sided-attrib))
        (gl/make-buffers-in-spec gl glc/static-draw)))
#+END_SRC

* COMMENT Model: Texture-Mapped Cube
** Shader

Pretty generic texture mapping.

#+BEGIN_SRC clojure
  (def shader-spec
    {:vs "void main() {
      vUV = uv;
      gl_Position = proj * view * model * vec4(position, 1.0);
      }"
     :fs "void main() {
      gl_FragColor = texture2D(tex, vUV);
      }"
     :uniforms {:model    [:mat4 M44]
                :view     :mat4
                :proj     :mat4
                :tex      :sampler2D}
     :attribs  {:position :vec3
                :uv       :vec2}
     :varying  {:vUV      :vec2}
     :state    {:depth-test false
                :blend      true
                :blend-fn   [glc/src-alpha glc/one]}})
#+END_SRC

** Model

#+BEGIN_SRC clojure
  (defn make-model [gl]
    (-> (a/aabb 1)
        (g/center)
        (g/as-mesh
         {:mesh    (glm/indexed-gl-mesh 12 #{:uv})
          ;;:flags   :nsb
          :attribs {:uv (attr/face-attribs (attr/uv-cube-map-v 256 false))}})
        (gl/as-gl-buffer-spec {})
        (assoc :shader (sh/make-shader-from-spec gl shader-spec))
        (gl/make-buffers-in-spec gl glc/static-draw)))
#+END_SRC

* Viewport Machinery

Rebuild view rectangle (on resize).

(We don't rebuild the GL context, which seems odd as Reagent would appear to be replacing the main canvas; perhaps Reagent is smart enough to just resize the one that's already there. We could replace the GL, but I'm not sure how/if the old one needs to be explicitly discarded first.)

#+BEGIN_SRC clojure
  (defn rebuild-viewport [app]
    (let [gl (:gl app)
          _  (gl/set-viewport gl {:p [0 0] :size [(.-innerWidth js/window) (.-innerHeight js/window)]})
          vr (gl/get-viewport-rect gl)]
      (assoc app
             :view-rect vr
             ;; :model (make-model gl vr)
             )))
#+END_SRC

* Setup (File Texture Version)

Setup. Generate a GL context (from DOM element ~main~), viewport rectangle and model, attach to the application. Also kick off the texture loading.

#+BEGIN_SRC clojure
  (defn init-app [_]
    (debug "INIT")
    (let [gl        (gl/gl-context "main")
          view-rect (gl/get-viewport-rect gl)
          model     (make-model gl)
          tex-ready (volatile! false)
          file-tex  (buf/load-texture gl {:callback (fn [tex img] (vreset! tex-ready true))
                                          :src      "img/cubev.png"
                                          :flip     true})]
      (swap! app merge {:gl        gl
                        :view-rect view-rect
                        :model     model
                        :img       (-> (fx/init-fx-quad gl)
                                       #_(assoc :shader thresh))
                        :tex-ready tex-ready
                        :file-tex  file-tex})))
#+END_SRC

* Loop (File Texture Version)

Animation loop. ~update-app~ returns truthy/falsey depending whether Reagent has signalled that the component has been unmounted. If our texture isn't loaded, do nothing (but keep the animation running)

#+BEGIN_SRC clojure
  (defn update-app [this]
    (fn [t frame]
      (when (:active (reagent/state this))
        (let [{:keys [gl view-rect model stream tex-ready file-tex]} @app]
          (when @tex-ready
            (gl/bind file-tex)
            (doto gl
              (gl/set-viewport view-rect)
              (gl/clear-color-and-depth-buffer col/GRAY 1)
              (gl/draw-with-shader
               (-> model
                   (cam/apply
                    (cam/perspective-camera
                     {:eye (vec3 0 0 1.25)
                      ;;:up (m/normalize (vec3 (Math/sin t) 1 0))
                      :fov 90
                      :aspect view-rect}))
                   (assoc-in [:uniforms :model]
                             (-> M44 (g/rotate-x t) (g/rotate-y (* t 2)))))))))

        true)))
#+END_SRC
