-*- mode: org; mode: visual-line; cider-buffer-ns: "thi-ng-geom-starter.video" -*-
#+STARTUP: indent
#+PROPERTY: header-args:clojure  :tangle video.cljs
#+PROPERTY: header-args:clojure+ :results value verbatim replace

* Contents                                                          :TOC_2_org:
 - [[Reference][Reference]]
   - [[Geom][Geom]]
   - [[Other][Other]]
 - [[Namespaces][Namespaces]]
 - [[Stream Setup][Stream Setup]]
 - [[Model: Texture-Mapped Cube][Model: Texture-Mapped Cube]]
   - [[Shader][Shader]]
   - [[Model][Model]]
 - [[Viewport Machinery][Viewport Machinery]]
 - [[Setup][Setup]]
 - [[Loop (Video Version)][Loop (Video Version)]]

* Reference
** Geom

- [[https://github.com/thi-ng/geom/blob/develop/src/gl/core.org][thi.ng.geom.core]] (~gl~) - [[https://gitter.im/thi-ng/geom][discussion]]
- [[https://github.com/thi-ng/geom/blob/develop/src/gl/buffers.org][thi.ng.geom.buffers]] (~buf~)

** Other

- [[http://learningthreejs.com/blog/2012/02/07/live-video-in-webgl/][Live Video in WebGL (via THREE.js)]]

* Namespaces

#+BEGIN_SRC clojure
  (ns thi-ng-geom-starter.video
    (:require [reagent.core :as reagent]
              [thi.ng.math.core :as m :refer [PI HALF_PI TWO_PI]]
              [thi.ng.geom.gl.core :as gl]
              [thi.ng.geom.gl.webgl.constants :as glc]
              [thi.ng.geom.gl.webgl.animator :as anim]
              [thi.ng.geom.gl.buffers :as buf]
              [thi.ng.geom.gl.fx :as fx]
              [thi.ng.geom.gl.shaders :as sh]
              [thi.ng.geom.gl.glmesh :as glm]
              [thi.ng.geom.gl.camera :as cam]
              [thi.ng.geom.gl.shaders :as sh]
              [thi.ng.geom.gl.shaders.lambert :as lambert]
              [thi.ng.geom.core :as g]
              [thi.ng.geom.vector :as v :refer [vec2 vec3]]
              [thi.ng.geom.matrix :as mat :refer [M44]]
              [thi.ng.geom.aabb :as a]
              [thi.ng.geom.plane :as p]
              [thi.ng.geom.attribs :as attr]
              [thi.ng.domus.core :as dom]
              [thi.ng.color.core :as col]
              [thi.ng.strf.core :as f]
              [thi-ng-geom-starter.shaders :as shaders])
      (:require-macros [cljs-log.core :refer [debug info warn severe]]))
#+END_SRC

#+RESULTS:
: nil

Application state. This used to be just set once, replaced via ~reset!~ when the initialisation was called on Reagent component mount. Now it's a bit smarter: we swap in the video stream once (if) it kicks off. (It's a Reagent atom so that we can show its state on-screen.)

#+BEGIN_SRC clojure
  (defonce app (reagent/atom {:stream {:state :wait}
                              :curr-shader :thresh}))
#+END_SRC

#+RESULTS:
: nil

* Stream Setup

Helper: set stream state:

#+BEGIN_SRC clojure
  (defn set-stream-state! [state]
    (swap! app assoc-in [:stream :state] state))
#+END_SRC

Set up video texture. Requires a GL context.

#+BEGIN_SRC clojure
  (defn init-video-texture [video]
    (let [tex (buf/make-canvas-texture
               (:gl @app)
               video
               {:filter      glc/linear
                :wrap        glc/clamp-to-edge
                :width       (.-width video)
                :height      (.-height video)
                :flip        true
                :premultiply false})]
      (debug "SWAPPING!")
      (swap! app assoc-in [:scene :img :shader :state :tex] tex)))
#+END_SRC

Activate the RTC stream.

#+BEGIN_SRC clojure
  (defn activate-rtc-stream [video stream]
    (swap! app assoc-in [:stream :video] video)
    (set! (.-onerror video)
          (fn [] (.stop stream) (set-stream-state! :error)))
    (set! (.-onended stream)
          (fn [] (.stop stream) (set-stream-state! :stopped)))
    (set! (.-src video)
          (.createObjectURL (or (aget js/window "URL") (aget js/window "webkitURL")) stream))
    (set-stream-state! :ready)
    (init-video-texture video))
#+END_SRC

Stream initialisation. This appears to kick off a hidden video player anchored to the document body.

#+BEGIN_SRC clojure
  (defn init-rtc-stream [w h]
    (let [video (dom/create-dom!
                 [:video {:width w :height h :hidden true :autoplay true}]
                 (.-body js/document))]
      (cond
        (aget js/navigator "webkitGetUserMedia")
        (.webkitGetUserMedia js/navigator #js {:video true}
                             #(activate-rtc-stream video %)
                             #(set-stream-state! :forbidden))

        (aget js/navigator "mozGetUserMedia")
        (.mozGetUserMedia js/navigator #js {:video true}
                          #(activate-rtc-stream video %)
                          #(set-stream-state! :forbidden))

        :else
        (set-stream-state! :unavailable))))
#+END_SRC

* Model: Texture-Mapped Cube
** Shader

Pretty generic texture mapping.

#+BEGIN_SRC clojure
  (def shader-spec
    {:vs "void main() {
      vUV = uv;
      gl_Position = proj * view * model * vec4(position, 1.0);
      }"
     :fs "void main() {
      gl_FragColor = texture2D(tex, vUV);
      }"
     :uniforms {:model    [:mat4 M44]
                :view     :mat4
                :proj     :mat4
                :tex      :sampler2D}
     :attribs  {:position :vec3
                :uv       :vec2}
     :varying  {:vUV      :vec2}
     :state    {:depth-test false
                :blend      true
                :blend-fn   [glc/src-alpha glc/one]}})
#+END_SRC

** Model

#+BEGIN_SRC clojure
  (defn make-model [gl]
    (-> (a/aabb 1)
        #_ (p/plane [0 0 0] 1)
        (g/center)
        (g/as-mesh {:mesh    (glm/indexed-gl-mesh 12 #{:uv})
                    :attribs {:uv attr/uv-faces}})
        (gl/as-gl-buffer-spec {})
        (assoc :shader (sh/make-shader-from-spec gl shaders/cube-shader-spec #_ shader-spec))
        (gl/make-buffers-in-spec gl glc/static-draw)))
#+END_SRC

* Viewport Machinery

Rebuild view rectangle (on resize).

(We don't rebuild the GL context, which seems odd as Reagent would appear to be replacing the main canvas; perhaps Reagent is smart enough to just resize the one that's already there. We could replace the GL, but I'm not sure how/if the old one needs to be explicitly discarded first.)

#+BEGIN_SRC clojure
  (defn rebuild-viewport [app]
    (let [gl (:gl app)
          _  (gl/set-viewport gl {:p [0 0] :size [(.-innerWidth js/window) (.-innerHeight js/window)]})
          vr (gl/get-viewport-rect gl)]
      (assoc app
             :view-rect vr
             ;; :model (make-model gl vr)
             )))
#+END_SRC

* Setup

Setup. Generate a GL context (from DOM element ~main~), viewport rectangle and model, attach to the application. Also kick off the texture loading.

#+BEGIN_SRC clojure
  (defn init-app
    [this]
    (let [vw        640
          vh        480
          gl        (gl/gl-context (reagent/dom-node this))
          view-rect (gl/get-viewport-rect gl)
          thresh    (sh/make-shader-from-spec gl shaders/threshold-shader-spec)
          hue-shift (sh/make-shader-from-spec gl shaders/hueshift-shader-spec)
          twirl     (sh/make-shader-from-spec gl shaders/twirl-shader-spec)
          pixelate  (sh/make-shader-from-spec gl shaders/pixelate-shader-spec)
          tile      (sh/make-shader-from-spec gl shaders/tile-shader-spec)
          fbo-tex   (buf/make-texture
                     gl {:width  512
                         :height 512
                         :filter glc/linear
                         :wrap   glc/clamp-to-edge})
          fbo       (buf/make-fbo-with-attachments
                     gl {:tex    fbo-tex
                         :width  512
                         :height 512
                         :depth? true})]
      (swap! app merge
             {:gl          gl
              :view        view-rect
              :shaders     {:thresh    thresh
                            :hue-shift hue-shift
                            :twirl     twirl
                            :tile      tile
                            :pixelate  pixelate}
              :scene       {:fbo     fbo
                            :fbo-tex fbo-tex
                            :cube    (make-model gl)
                            :img     (-> (fx/init-fx-quad gl)
                                         #_(assoc :shader thresh))}})
      (init-rtc-stream vw vh)))
#+END_SRC

* Loop (Video Version)

#+BEGIN_SRC clojure
  (def try-it true)

  (defn update-app
    [this]
    (fn [t frame]
      (let [{:keys [gl view scene stream shaders curr-shader]} @app]
        ;;(debug "frame with tex?" (str (get-in scene [:img :shader])))
        (when-let [tex (get-in scene [:img :shader :state :tex])]
          (gl/configure tex {:image (:video stream)})
          (gl/bind tex)
          ;; render to texture
          (when try-it (gl/bind (:fbo scene)))
          (doto gl
            (gl/set-viewport 0 0 512 512)
            (gl/clear-color-and-depth-buffer col/BLACK 1)
            (gl/draw-with-shader
             (-> (:img scene)
                 (assoc-in [:uniforms :time] t)
                 (assoc :shader (shaders curr-shader)))))
          (when try-it (gl/unbind (:fbo scene)))
          ;; render cube to main canvas
          (when try-it
            (gl/bind (:fbo-tex scene) 0)
            (doto gl
              (gl/set-viewport view)
              (gl/draw-with-shader
               (-> (:cube scene)
                   (cam/apply
                    (cam/perspective-camera
                     {:eye (vec3 0 0 1.25) :fov 90 :aspect view}))
                   (assoc-in [:uniforms :model] (-> M44 (g/rotate-x t) (g/rotate-y (* t 2)))))))))
        (:active (reagent/state this)))))
#+END_SRC
